#version 440

layout (location = 0) in vec2 koseKonumu;
layout (location = 1) in vec2 dokuKonumu;
layout (location = 2) in vec3 konumu;
layout (location = 3) in vec2 boyutu;
layout (location = 4) in float acisi;

out vec2 beneginDokuKonumu;

// Izdusum
uniform vec3 izdusum;

// Bakis
uniform vec3 bakisKonumu;
uniform float bakisBoyutu;
uniform float bakisAcisi;

void main() {
	// Kendi merkezinden oranla.
	vec2 olcekliKonumu = koseKonumu * boyutu;

	// Kendi merkezi etrafinda cevir.
	vec2 nesneKonumu;
	float dikmeligi = sin(acisi);
	float esdikmeligi = cos(acisi);
	nesneKonumu.x = olcekliKonumu.x * esdikmeligi - olcekliKonumu.y * dikmeligi;
	nesneKonumu.y = olcekliKonumu.x * dikmeligi + olcekliKonumu.y * esdikmeligi;

	// Kendi merkezinden oranla ve ekran konumuna tasi.
	vec3 ekranKonumu = konumu - bakisKonumu + vec3(nesneKonumu, 0.0);
	
	// Bakanin etrafinda cevir.
	vec3 gorunusKonumu;
	dikmeligi = sin(bakisAcisi);
	esdikmeligi = cos(bakisAcisi);
	gorunusKonumu.x = ekranKonumu.x * esdikmeligi + ekranKonumu.y * dikmeligi;
	gorunusKonumu.y = ekranKonumu.y * esdikmeligi - ekranKonumu.x * dikmeligi;
	gorunusKonumu.z = ekranKonumu.z;

	// Bakanin merkezinden oranla ve izdusumunu bul.
    gl_Position = vec4(gorunusKonumu * bakisBoyutu / izdusum * 2.0, 1.0);
    beneginDokuKonumu = dokuKonumu;
}
