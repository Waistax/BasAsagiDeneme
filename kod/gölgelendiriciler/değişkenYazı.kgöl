#version 440

// Model
layout (location = 0) in vec2 nitelikKoseKonumu;
layout (location = 1) in vec2 nitelikDokuKonumu0;
layout (location = 2) in vec2 nitelikDokuKonumu1;
layout (location = 3) in vec2 nitelikDokuKonumu2;
layout (location = 4) in vec2 nitelikDokuKonumu3;

// Donusum
layout (location = 5) in vec3 nitelikKonumu;
layout (location = 6) in vec2 nitelikBoyutu;
layout (location = 7) in float nitelikAcisi;

out vec2 beneginDokuKonumu;

// Izdusum
uniform vec3 izdusum;

// Bakis
uniform vec3 bakisKonumu;
uniform float bakisBoyutu;
uniform float bakisAcisi;

void main() {
	// Kendi merkezinden oranla.
	vec2 olcekliKonumu = nitelikKoseKonumu * nitelikBoyutu;

	// Kendi merkezi etrafinda cevir.
	vec2 nesneKonumu;
	float dikmeligi = sin(nitelikAcisi);
	float esdikmeligi = cos(nitelikAcisi);
	nesneKonumu.x = olcekliKonumu.x * esdikmeligi - olcekliKonumu.y * dikmeligi;
	nesneKonumu.y = olcekliKonumu.x * dikmeligi + olcekliKonumu.y * esdikmeligi;

	// Kendi merkezinden oranla ve ekran konumuna tasi.
	vec3 ekranKonumu = nitelikKonumu - bakisKonumu + vec3(nesneKonumu, 0.0);
	
	// Bakanin etrafinda cevir.
	vec3 gorunusKonumu;
	dikmeligi = sin(bakisAcisi);
	esdikmeligi = cos(bakisAcisi);
	gorunusKonumu.x = ekranKonumu.x * esdikmeligi + ekranKonumu.y * dikmeligi;
	gorunusKonumu.y = ekranKonumu.y * esdikmeligi - ekranKonumu.x * dikmeligi;

	// Bakanin merkezinden oranla.
	gorunusKonumu *= bakisBoyutu;
	gorunusKonumu.z = ekranKonumu.z;

	// Izdusumunu bul.
    gl_Position = vec4(gorunusKonumu / izdusum * 2.0, 1.0);
	
    // Doku konumunu aktar.
    switch (gl_VertexID) {
    	case 0: beneginDokuKonumu = nitelikDokuKonumu0; break;
    	case 1: beneginDokuKonumu = nitelikDokuKonumu1; break;
    	case 2: beneginDokuKonumu = nitelikDokuKonumu2; break;
    	case 3: beneginDokuKonumu = nitelikDokuKonumu3; break;
    };
}
